const std = @import("std");
const models = @import("../../models.zig");
const cli = @import("../../cli.zig");
const detector = @import("../../detector.zig");
const converter = @import("../converter.zig");
const default_output_file: []const u8 = "generated.zig";

pub const ApiCodeGenerator = struct {
    allocator: std.mem.Allocator,
    args: cli.CliArgs,

    pub fn init(allocator: std.mem.Allocator, args: cli.CliArgs) ApiCodeGenerator {
        return ApiCodeGenerator{
            .allocator = allocator,
            .args = args,
        };
    }

    pub fn deinit(self: *ApiCodeGenerator) void {
        _ = self;
    }

    pub fn generate(self: *ApiCodeGenerator, document: models.SwaggerDocument) ![]const u8 {
        var parts = std.ArrayList([]const u8).empty;
        defer parts.deinit(self.allocator);
        var methods = std.ArrayList([]const u8).empty;
        defer methods.deinit(self.allocator);
        try parts.append(self.allocator, "///////////////////////////////////////////\n");
        try parts.append(self.allocator, "// Generated Zig API client from Swagger v2.0\n");
        try parts.append(self.allocator, "///////////////////////////////////////////\n\n");
        try parts.append(self.allocator, "const std = @import(\"std\");\n\n");
        var path_iterator = document.paths.path_items.iterator();
        while (path_iterator.next()) |entry| {
            const path_key = entry.key_ptr.*;
            const path_item = entry.value_ptr.*;
            var base_url: []const u8 = "";
            var allocated_base_url = false;
            if (self.args.base_url) |base| {
                base_url = base;
            } else if (document.host) |host| {
                const scheme = if (document.schemes != null and document.schemes.?.len > 0) document.schemes.?[0] else "https";
                const base_path = document.basePath orelse "";
                base_url = try std.fmt.allocPrint(self.allocator, "{s}://{s}{s}", .{ scheme, host, base_path });
                allocated_base_url = true;
            }
            defer if (allocated_base_url) self.allocator.free(base_url);
            const path = if (base_url.len > 0) try std.fmt.allocPrint(self.allocator, "{s}{s}", .{ base_url, path_key }) else path_key;
            defer if (base_url.len > 0) self.allocator.free(path);
            if (path_item.get) |op| {
                try methods.append(self.allocator, try self.generateMethod(op, path, "GET"));
            }
            if (path_item.post) |op| {
                try methods.append(self.allocator, try self.generateMethod(op, path, "POST"));
            }
            if (path_item.put) |op| {
                try methods.append(self.allocator, try self.generateMethod(op, path, "PUT"));
            }
            if (path_item.delete) |op| {
                try methods.append(self.allocator, try self.generateMethod(op, path, "DELETE"));
            }
            if (path_item.patch) |op| {
                try methods.append(self.allocator, try self.generateMethod(op, path, "PATCH"));
            }
            if (path_item.head) |op| {
                try methods.append(self.allocator, try self.generateMethod(op, path, "HEAD"));
            }
            if (path_item.options) |op| {
                try methods.append(self.allocator, try self.generateMethod(op, path, "OPTIONS"));
            }
        }
        for (methods.items) |method| {
            try parts.append(self.allocator, method);
        }
        const code = try std.mem.join(self.allocator, "", parts.items);
        for (methods.items) |method| {
            self.allocator.free(method);
        }
        return code;
    }

    pub fn generateMethod(self: *ApiCodeGenerator, op: models.v2.Operation, path: []const u8, method: []const u8) ![]const u8 {
        var parts = std.ArrayList([]const u8).empty;
        defer parts.deinit(self.allocator);
        const comments = try generateMethodDocs(self.allocator, op);
        defer self.allocator.free(comments);
        try parts.append(self.allocator, comments);
        try parts.append(self.allocator, "pub fn ");
        try parts.append(self.allocator, op.operationId orelse path);
        try parts.append(self.allocator, "(allocator: std.mem.Allocator, io: std.Io");
        var path_parameters = std.ArrayList([]const u8).empty;
        defer path_parameters.deinit(self.allocator);
        var has_body_param = false;
        if (op.parameters) |params| {
            if (params.len > 0) try parts.append(self.allocator, ", ");
            var first = true;
            for (params) |param| {
                if (!first) try parts.append(self.allocator, ", ");
                first = false;
                var data_type: []const u8 = "[]const u8"; // Default to string
                var name: []const u8 = param.name;
                if (param.in == .body) {
                    has_body_param = true;
                    name = "requestBody";
                    if (param.schema.?.ref) |ref| {
                        if (extractTypeFromReference(ref)) |type_name| {
                            data_type = type_name;
                        }
                    }
                } else if (param.in == .path) {
                    try path_parameters.append(self.allocator, name);
                }
                if (param.type) |param_type| {
                    data_type = converter.getDataType(@tagName(param_type));
                }
                try parts.append(self.allocator, name);
                try parts.append(self.allocator, ": ");
                try parts.append(self.allocator, data_type);
            }
        }
        try parts.append(self.allocator, ") !void {\n");
        const method_body = try generateImplementation(self.allocator, path, method, op, has_body_param);
        defer self.allocator.free(method_body);
        try parts.append(self.allocator, method_body);
        try parts.append(self.allocator, "}\n\n");
        return try std.mem.join(self.allocator, "", parts.items);
    }
};

fn extractTypeFromReference(ref: []const u8) ?[]const u8 {
    const prefix = "#/definitions/";
    if (std.mem.startsWith(u8, ref, prefix)) {
        return ref[prefix.len..];
    }
    return null;
}

fn generateMethodDocs(allocator: std.mem.Allocator, op: models.v2.Operation) ![]const u8 {
    var parts = std.ArrayList([]const u8).empty;
    defer parts.deinit(allocator);
    if (op.summary) |summary| {
        try parts.append(allocator, "/// ");
        try parts.append(allocator, summary);
        try parts.append(allocator, "\n");
    }
    if (op.description) |description| {
        try parts.append(allocator, "/// ");
        try parts.append(allocator, description);
        try parts.append(allocator, "\n");
    }
    return try std.mem.join(allocator, "", parts.items);
}

fn generateImplementation(allocator: std.mem.Allocator, path: []const u8, method: []const u8, op: models.v2.Operation, has_request_body: bool) ![]const u8 {
    var parts = std.ArrayList([]const u8).empty;
    defer parts.deinit(allocator);
    if (op.parameters) |parameters| {
        if (parameters.len > 0) {
            for (parameters) |parameter| {
                if (parameter.in != .path and parameter.in != .body) {
                    try parts.append(allocator, "    _ = ");
                    try parts.append(allocator, parameter.name);
                    try parts.append(allocator, ";\n");
                }
            }
            if (has_request_body) {
                try parts.append(allocator, "\n");
            }
            try parts.append(allocator, "\n");
        }
    }
    try parts.append(allocator, "    var client: std.http.Client = .{ .allocator = allocator, .io = io };\n");
    try parts.append(allocator, "    defer client.deinit();\n\n");
    var allocations = std.ArrayList([]const u8).empty;
    defer allocations.deinit(allocator);
    if (op.parameters) |parameters| {
        if (parameters.len > 0) {
            var new_path = path;
            for (parameters) |parameter| {
                if (parameter.in != .path) continue;
                const param = parameter.name;
                const size = std.mem.replacementSize(u8, new_path, param, "any");
                const output = try allocator.alloc(u8, size);
                _ = std.mem.replace(u8, new_path, param, "any", output);
                new_path = output;
                try allocations.append(allocator, output);
            }
            try parts.append(allocator, "    const uri_str = try std.fmt.allocPrint(allocator, \"");
            try parts.append(allocator, new_path);
            try parts.append(allocator, "\", .{");
            var pos: i32 = 0;
            for (parameters) |parameter| {
                if (parameter.in != .path) continue;
                const param = parameter.name;
                try parts.append(allocator, param);
                pos += 1;
                if (pos < parameters.len)
                    try parts.append(allocator, ", ");
            }
            try parts.append(allocator, "});\n");
            try parts.append(allocator, "    defer allocator.free(uri_str);\n\n");
            try parts.append(allocator, "    const uri = try std.Uri.parse(uri_str);\n");
        } else {
            try parts.append(allocator, "    const uri = try std.Uri.parse(\"");
            try parts.append(allocator, path);
            try parts.append(allocator, "\");\n");
        }
    }
    try parts.append(allocator, "\n");
    try parts.append(allocator, "    var req = try client.request(.");
    try parts.append(allocator, method);
    try parts.append(allocator, ", uri, .{});\n");
    try parts.append(allocator, "    defer req.deinit();\n");
    if (has_request_body) {
        try parts.append(allocator, "\n");
        try parts.append(allocator, "    var str: std.Io.Writer.Allocating = .init(allocator);\n");
        try parts.append(allocator, "    defer str.deinit();\n\n");
        try parts.append(allocator, "    try std.json.Stringify.value(requestBody, .{}, &str.writer);\n");
        try parts.append(allocator, "    const body = str.written();\n\n");
        try parts.append(allocator, "    try req.sendBodyComplete(body);\n");
    } else {
        try parts.append(allocator, "\n    try req.sendBodiless();\n");
    }
    const code = try std.mem.join(allocator, "", parts.items);
    for (allocations.items) |alloc| {
        allocator.free(alloc);
    }
    return code;
}
